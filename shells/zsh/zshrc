export DF="$HOME/.dotfiles"
export PLATFORM="$(uname -s)"
export HOMEBREW_INSTALL_CLEANUP=true

#-------------------------------Bootstrap homebrew[s]
[[ $PLATFORM == 'Darwin' ]] && [[ "$(arch)" == "i386" ]] && [[ -f /usr/local/bin/brew ]] && eval "$(/usr/local/bin/brew shellenv)"
[[ $PLATFORM == 'Darwin' ]] && [[ "$(arch)" == "arm64" ]] && [[ -d /opt/homebrew ]] && eval "$(/opt/homebrew/bin/brew shellenv)"
[[ $PLATFORM == 'Linux' ]] && [[ -d /home/linuxbrew/.linuxbrew ]] && eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv)"

#-------------------------------PREFER nvim
if [[ -f $(which nvim) ]]; then
	export EDITOR='nvim'
elif [[ -f $(which vim) ]]; then
	export EDITOR='vim'
fi

#-------------------------------ZSH OPTIONS
COMPLETION_WAITING_DOTS="true"
DIRSTACKSIZE=20 # pushd stacksize
setopt AUTO_PUSHD PUSHD_IGNORE_DUPS PUSHD_MINUS PUSHD_SILENT 
HISTSIZE=20000
SAVEHIST=20000
HISTFILE=~/.zsh_history
setopt extended_glob
setopt extended_history 
setopt hist_expire_dups_first	# Expire duplicate entries first when trimming history.
setopt hist_ignore_dups			# Don't record an entry that was just recorded again.
setopt hist_ignore_all_dups		# Delete old recorded entry if new entry is a duplicate.
setopt hist_ignore_space		# Don't record an entry starting with a space.
setopt hist_save_no_dups		# Don't write duplicate entries in the history file.
setopt hist_reduce_blanks		# Remove superfluous blanks before recording entry.
setopt inc_append_history		# write to the history file immediately, not when the shell exits.
setopt share_history			# share command history data
export MANPAGER='less -X'		# don't clear after quitting man
typeset -U PATH path			# don't allow duplicates in path

unalias run-help >& /dev/null
autoload run-help # use ESC + H to bring up help, see https://man.archlinux.org/man/extra/zsh/zshcontrib.1.en
alias help=run-help

#------------------------------------PATHS/ENVS ETC.
([[ -x $(which brew) ]] && [[ -d "$(brew --prefix)/share/zsh/site-functions/" ]]) && fpath=("$(brew --prefix)/share/zsh/site-functions/" $fpath)

jl=("temurin-25-jdk" "temurin-21-jdk" "temurin-17-jdk" "java-21-openjdk" "java-17-openjdk")
for j in $jl; do
	if [[ -d "/usr/lib/jvm/${j}-amd64" ]]; then
		export JAVA_HOME="/usr/lib/jvm/${j}-amd64"
		export MATLAB_JAVA="/usr/lib/jvm/${j}-amd64"
		path=("${JAVA_HOME}/bin" $path) 
		break
	fi
done

ul=("R2025b" "R2025a" "R2024b" "R2024a" "R2023b" "R2023a" "R2022b" "R2022a" "R2021b" "R2021a" "R2020b" "R2020a")
if [[ $PLATFORM == 'Darwin' ]]; then
	for x in $ul; do
		if [[ -d "/Applications/MATLAB_${x}.app/bin" ]]; then
			path=("/Applications/MATLAB_${x}.app/bin" $path) # matlab
			export MATLAB_EXECUTABLE="/Applications/MATLAB_${x}.app/bin/matlab" # matlab
			ln -sf "/Applications/MATLAB_${x}.app/bin/maci64/mlint" ~/bin/mlint # matlab
			break
		fi
	done
else
	for x in $ul; do
		if [[ -d "/usr/local/MATLAB/${x}/bin" ]]; then
			path=("/usr/local/MATLAB/${x}/bin" $path) # matlab
			export MATLAB_EXECUTABLE="/usr/local/MATLAB/${x}/bin/matlab" # matlab
			ln -sf "/usr/local/MATLAB/${x}/bin/glnxa64/mlint" ~/bin/mlint &> /dev/null # matlab
			break
		fi
	done
fi
#------------------------------------LUA
[[ -d "$HOME/.local/share/pandoc/filters" ]] && export LUA_PATH="$HOME/.local/share/pandoc/filters/?.lua;;"
[[ -d /opt/homebrew/share/lua/5.4 ]] && export LUA_PATH="/opt/homebrew/share/lua/5.4/?.lua;$LUA_PATH"
[[ -d /opt/homebrew/lib/lua/5.4 ]] && export LUA_CPATH="/opt/homebrew/lib/lua/5.4/?.so;/opt/homebrew/lib/lua/5.4/?/?.so;;"

#------------------------------------FINALISE PATH
pth=("/Library/TeX/texbin" "$HOME/Library/TinyTeX/bin/universal-darwin"
	"/opt/amdgpu-pro/bin" "opt/amdgpu/bin"
	"/usr/local/sbin" "/usr/local/bin"
	"/opt/homebrew/opt/ruby/bin" "/opt/homebrew/lib/ruby/gems/3.4.0/bin"
	"/snap/bin" "$HOME/.local/bin" 
	"$HOME/.cache/lm-studio/bin"
	"$HOME/.antigravity/antigravity/bin"
	"$HOME/.pixi/envs/ruby/share/rubygems/bin"
	"$HOME/.pixi/bin" "$HOME/bin")
for p in $pth; do
	if [[ -d $p ]]; then
		path=($p $path)
	fi
done
export path

#------------------------------------X-CMD SETUP
export ___X_CMD_LANG="en"
[ ! -f "$HOME/.x-cmd.root/X" ] || . "$HOME/.x-cmd.root/X" # boot up x-cmd.

#------------------------------------FINALISE OTHERS
[[ -x $(which fzf) ]] && source <(fzf --zsh)
[[ -x $(which pixi) ]] && eval "$(pixi completion --shell zsh)"
[[ -x $(which zoxide) ]] && eval "$(zoxide init zsh)"
[[ -x $(which rotz) ]] && eval "$(rotz completions zsh)"
[[ -f "$DF/shells/aliases" ]] && source "$DF/shells/aliases"

### Added by Zinit's installer
if [[ ! -f $HOME/.local/share/zinit/zinit.git/zinit.zsh ]]; then
    print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})â€¦%f"
    command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$HOME/.local/share/zinit/zinit.git" && \
        print -P "%F{33} %F{34}Installation successful.%f%b" || \
        print -P "%F{160} The clone has failed.%f%b"
fi
source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load a few important annexes, without Turbo
# (this is currently required for annexes)
zinit light-mode for \
	zdharma-continuum/zinit-annex-as-monitor \
	zdharma-continuum/zinit-annex-bin-gem-node \
	zdharma-continuum/zinit-annex-patch-dl \
	zdharma-continuum/zinit-annex-rust \
	z-shell/F-Sy-H \
	zsh-users/zsh-autosuggestions \
	marlonrichert/zsh-autocomplete
#

### End of Zinit's installer chunk
# marlonrichert/zsh-autocomplete

#---------------------------------------STARSHIP
if [[ -f $(which starship) ]]; then
	eval "$(starship init zsh)"
else
	autoload -Uz promptinit; promptinit; prompt fire
fi

#---------------------------------------SAY HELLO
echo "\nâŒƒa,e: â‡„ | âŒƒw,k,u: ðŸ”ª | âŒƒr,s: ðŸ” | d, cd - & cd #n: ðŸš€ | ðŸ’¡ curl cheat.sh/?\n"
#---------------------------------------END OF FILE